/**
 * Root TypoScript template for the sfgz.ch site (signalwerk.sfgz)
 */

include: ./**/*.fusion

// https://discuss.neos.io/t/solved-problem-with-selecting-page-layout/3348/4
// https://github.com/neos/neos-development-collection/pull/1973
root.layout {
  @position = 'end 9997'
  layout = ${q(node).property('layout') != null && q(node).property('layout') != '' ? q(node).property('layout') : q(node).parents('[subpageLayout][subpageLayout != ""]').first().property('subpageLayout')}
  condition = ${this.layout != null && this.layout != ''}
  renderPath = ${'/' + this.layout}
}

prototype(Neos.NodeTypes:Page) < prototype(Neos.Neos:Page) {

  head {

    signalwerk = Neos.Fusion:Template {
      templatePath = 'resource://signalwerk.sfgz/Private/Templates/Page/Default.html'
      sectionName = 'signalwerk'
    }

    stylesheets.site = Neos.Fusion:Template {
      templatePath = 'resource://signalwerk.sfgz/Private/Templates/Page/Default.html'
      sectionName = 'stylesheets'
    }

    javascripts.site = Neos.Fusion:Template {
      templatePath = 'resource://signalwerk.sfgz/Private/Templates/Page/Default.html'
      sectionName = 'headScripts'
    }

    javascripts.JobWidget = '<script src="//pub.refline.ch/100140/refline.js"></script>'
    // javascripts.JobWidget.@if.isNeeded = ${q(documentNode).children('main').find('[instanceof signalwerk.sfgz:Content.JobWidget]').is()}

    meta = Neos.Fusion:Template {
      templatePath = 'resource://signalwerk.sfgz/Private/Templates/Page/Default.html'
      sectionName = 'meta'
    }
  }

  bodyTag.attributes.class = ${q(node).property('style')}

  body {

    workspaceName = ${node.context.workspace.name}
    contextName = ${Configuration.setting('Neos.Flow.core.context')}

    templatePath = 'resource://signalwerk.sfgz/Private/Templates/Page/Default.html'
    sectionName = 'body'

    parts {

      // Schule für Gestaltung Zürich
      headerTitle = ${q(node).closest("[instanceof signalwerk.sfgz:Root][headerTitle][headerTitle != '']").property('headerTitle')}

      menu1 = Neos.Neos:Menu {
        templatePath = 'resource://signalwerk.sfgz/Private/Templates/FusionObjects/DefaultMenu.html'

        entryLevel = 2
        maximumLevels = 1
        startingPoint = ${q(site).children('[uriPathSegment="angebot"]').get(0)}
      }

      menu2 = Neos.Neos:Menu {
        templatePath = 'resource://signalwerk.sfgz/Private/Templates/FusionObjects/DefaultMenu.html'

        entryLevel = 3
        maximumLevels = 1
      }

      menu3 = Neos.Neos:Menu {
        templatePath = 'resource://signalwerk.sfgz/Private/Templates/FusionObjects/DefaultMenu.html'

        entryLevel = 4
        maximumLevels = 1
        attributes.class = 'menu menu3'
      }

      menuSiblings = Neos.Neos:Menu {
        templatePath = 'resource://signalwerk.sfgz/Private/Templates/FusionObjects/DefaultMenu.html'
        entryLevel = ${q(node).property('_depth') - 2}
        maximumLevels = 1
      }

      currentLevel = ${q(node).property('_depth') - 2}

      mobileMenu1 = Neos.Neos:Menu {
        templatePath = 'resource://signalwerk.sfgz/Private/Templates/FusionObjects/DefaultMenu.html'

        entryLevel = 2
        maximumLevels = 1
        startingPoint = ${q(site).children('[uriPathSegment="angebot"]').get(0)}
      }

      mobileMenu2 = Neos.Neos:Menu {
        templatePath = 'resource://signalwerk.sfgz/Private/Templates/FusionObjects/DefaultMenu.html'

        entryLevel = 3
        maximumLevels = 2
      }

      metaMenu = Neos.Neos:Menu {
        templatePath = 'resource://signalwerk.sfgz/Private/Templates/FusionObjects/DefaultMenu.html'

        entryLevel = 2
        maximumLevels = 1
        startingPoint = ${q(site).children('[uriPathSegment="schule"]').get(0)}
      }
    }

    hasExtras = ${q(node).children('extra').children().count() > 0}

    // These are your content areas, you can define as many as you want, just name them and the nodePath.
    content {
      // The default content section
      main = Neos.Neos:PrimaryContent {
        nodePath = 'main'
      }

      extra = Neos.Neos:ContentCollection {
        nodePath = 'extra'
      }
    }

    javascripts.site = Neos.Fusion:Template {
      templatePath = 'resource://signalwerk.sfgz/Private/Templates/Page/Default.html'
      sectionName = 'bodyScripts'
    }


    javascripts.main = Neos.Fusion:Tag {
      tagName = 'script'
      attributes {
        src = Neos.Fusion:ResourceUri {
          package = 'signalwerk.sfgz'
          path = 'JavaScript/main.js'
        }
        async = null
      }
    }

    javascripts.backend = Neos.Fusion:Tag {
      tagName = 'script'
      attributes {
        src = Neos.Fusion:ResourceUri {
          package = 'signalwerk.sfgz'
          path = 'JavaScript/signalwerkBackend.js'
        }
        async = null
      }
      @if.onlyRenderWhenNotInLiveWorkspace = ${node.context.workspace.name != 'live'}
    }

    javascripts.frontend = Neos.Fusion:Tag {
      tagName = 'script'
      attributes {
        src = Neos.Fusion:ResourceUri {
          package = 'signalwerk.sfgz'
          path = 'JavaScript/signalwerkFrontend.js'
        }
        async = null
      }
      @if.onlyRenderWhenNotInLiveWorkspace = ${node.context.workspace.name == 'live'}
    }
  }
}

page = Neos.NodeTypes:Page


default < page

// Special page layout with less navigation components
wide < page
wide {
  bodyTag.attributes.class = 'wide'
}

blog < page
blog {
  bodyTag.attributes.class = 'blog'
}


// https://discuss.neos.io/t/how-can-i-pass-an-variable-via-url-parameters/994/6
course < page
course {
  bodyTag.attributes.class = 'course'
  @cache.entryIdentifier.courseId = ${request.arguments.kurs}
  @cache.entryIdentifier.executionId = ${request.arguments.durchfuehrung}
  @cache.entryIdentifier.categoryId = ${request.arguments.cat}
}



#
# Add the paginator's "current page" value to cache entry identifiers in cache layers above the blog posts index.
# Depending on your actual page structure you will have to add this identifier part to further TypoScript object
# cache configurations.
#
root.@cache.entryIdentifier.courseId = ${request.arguments.kurs}
root.@cache.entryIdentifier.executionId = ${request.arguments.durchfuehrung}
root.@cache.entryIdentifier.categoryId = ${request.arguments.cat}

// prototype(Neos.Neos:Page) {
//   @cache.entryIdentifier.courseId = ${request.arguments.kurs}
// }

prototype(Neos.Neos:PrimaryContent).default {
  renderer.@cache.entryIdentifier.courseId = ${request.arguments.kurs}
  renderer.@cache.entryIdentifier.executionId = ${request.arguments.durchfuehrung}
  renderer.@cache.entryIdentifier.categoryId = ${request.arguments.cat}
}
